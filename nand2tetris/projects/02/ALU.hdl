// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=s1x);
    Mux16(a=y, b=false, sel=zy, out=s1y);

    // stage 1 output s1x and s1y

    Not16(in=s1x, out=negatedx);
    Not16(in=s1y, out=negatedy);

    Mux16(a=s1x, b=negatedx, sel=nx, out=s2x);
    Mux16(a=s1y, b=negatedy, sel=ny, out=s2y);

    // stage 2 output s2x and s2y

    And16(a=s2x, b=s2y, out=andedfx);
    Add16(a=s2x, b=s2y, out=addedfx);
    Mux16(a=andedfx, b=addedfx, sel=f, out=s3);

    // stage 3 output s3
    Not16(in=s3, out=s3n);
    Mux16(a=s3, b=s3n, sel=no, out=t1);

    // set the other inputs by comparison
    Or16Way(in=t1, out=w3);
    Not(in=w3, out=zr);

    // set the negative bit
    TopBit(in=t1, out=ng);
    Or16(a=t1, b=false, out=out);
}